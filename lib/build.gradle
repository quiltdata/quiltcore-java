import com.vanniktech.maven.publish.SonatypeHost

plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id 'com.vanniktech.maven.publish' version '0.30.0'
    id 'se.patrikerdes.use-latest-versions' version '0.2.18'
    id 'com.github.ben-manes.versions' version '0.51.0'
}

useLatestVersions {
   // A blacklist of dependencies to update, in the format of group:name
   updateBlacklist = [
   ]
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    // AWS SDK
    implementation 'software.amazon.awssdk:s3:2.27.24'
    implementation 'software.amazon.awssdk:s3-transfer-manager:2.27.24'
    implementation 'software.amazon.awssdk.crt:aws-crt:0.30.9'

    // JSON and YAML parsing.
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.17.2'

    // JSON Schema validator.
    implementation 'io.vertx:vertx-json-schema:4.5.9'

    // Workarounds for Java's checked exceptions.
    implementation 'com.pivovarit:throwing-function:1.5.1'

    // Use JUnit test framework.
    testImplementation('org.junit.jupiter:junit-jupiter:5.11.0')

    // Add Log4j dependency
    implementation 'org.apache.logging.log4j:log4j-core:3.0.0-beta2'
    implementation 'org.apache.logging.log4j:log4j-api:3.0.0-beta2'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:3.0.0-beta2'
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

// Enable Java 11 compatibility to support NextFlow.
tasks {
    sourceCompatibility = '11'
    targetCompatibility = '11'
}

compileJava {
    options.release.set(11)
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)

    signAllPublications()

    coordinates('com.quiltdata', 'quiltcore', '0.1.5')

    pom {
        name.set('QuiltCore')
        description.set('Java implementation of the Quilt API')
        url.set('https://quiltdata.com')
        licenses {
            license {
                name.set('Apache License, Version 2.0')
                url.set('http://www.apache.org/licenses/LICENSE-2.0.txt')
            }
        }
        developers {
            developer {
                id.set('dima')
                name.set('Dima Ryazanov')
                email.set('dima@quiltdata.io')
            }
            developer {
                id.set('drernie')
                name.set('Ernest Prabhakar')
                email.set('ernest@quiltdata.io')
            }
        }
        scm {
            url = "https://github.com/quiltdata/quiltcore-java"
            connection = "scm:git:git://github.com/quiltdata/quiltcore-java.git"
            developerConnection = "scm:git:ssh://git@github.com/quiltdata/quiltcore-java.git"
        }
    }
}
